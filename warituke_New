'microsoft365のパワポで番号が想定通りに動かないことがあったので、修正版のコードを作成(アドインには反映できていない)

Option Explicit

Sub waritukeNew()
    Dim SaveDir As String
    Dim fileName As String
    Dim fileNo As Integer
    Dim fileDir As String

    fileDir = ActivePresentation.Path
    SaveDir = fileDir & "\tmp"
    
    '◆ tmpフォルダを準備
    If Dir(SaveDir, vbDirectory) = "" Then
        MkDir SaveDir
        fileNo = FreeFile
        Open SaveDir & "\LogFile.txt" For Output As #fileNo
        Print #fileNo, "このファイルは削除しないでください。"
        Close #fileNo
    Else
        If PathIsDirectoryEmpty(SaveDir) <> 1 Then
            Kill SaveDir & "\*"
            fileNo = FreeFile
            Open SaveDir & "\LogFile.txt" For Output As #fileNo
            Print #fileNo, "このファイルは削除しないでください。"
            Close #fileNo
        End If
    End If

    '◆ 元ファイル名（拡張子除去）
    With ActivePresentation
        fileName = Left(.Name, InStrRev(.Name, ".") - 1)
    End With
    
    '◆ 選択されたスライドを取得
    Dim sr As SlideRange
    With ActiveWindow.Selection
        If .Type >= ppSelectionSlides Then
            Set sr = .SlideRange
        Else
            MsgBox "スライドが選択されていません。"
            Exit Sub
        End If
    End With
    
    '――スライドを配列に取り込み――
    Dim arrSlides() As Slide
    ReDim arrSlides(1 To sr.Count)
    Dim i As Long
    For i = 1 To sr.Count
        Set arrSlides(i) = sr(i)
    Next i
    
    '――配列内でバブルソート――
    Dim j As Long
    Dim tmpSlide As Slide
    For i = 1 To UBound(arrSlides) - 1
        For j = i + 1 To UBound(arrSlides)
            If arrSlides(j).SlideIndex < arrSlides(i).SlideIndex Then
                '入れ替え
                Set tmpSlide = arrSlides(i)
                Set arrSlides(i) = arrSlides(j)
                Set arrSlides(j) = tmpSlide
            End If
        Next j
    Next i
    
    '――ソート後の順番でエクスポート――
    Debug.Print "選択スライド数: " & sr.Count
    For i = 1 To UBound(arrSlides)
        arrSlides(i).Export SaveDir & "\" & fileName & Format(i, "0000") & ".jpg", "JPG"
    Next i
    
    '――新規パワポを作成――
    Dim tmpPrs As Presentation
    Set tmpPrs = Presentations.Add
    tmpPrs.Slides.Add Index:=1, Layout:=ppLayoutBlank

    ' A4 サイズへ
    ActivePresentation.PageSetup.SlideSize = ppSlideSizeA4Paper
    
    '――割付配置――
    Call addPhotoTilingSlide(SaveDir)
    
    '――PDF で保存――
    With ActivePresentation
        .Export fileDir & "\_配布用_" & fileName & ".pdf", "PDF"
    End With
    tmpPrs.SaveAs fileDir & "\_配布用_" & fileName
    
    MsgBox "A4サイズの割付済PDFを作成しました。PDFファイルも同じフォルダに作成されています。"
End Sub
